file: 'package' pkg=IDENT ';' imp=import_decl* decl=(function_decl | method_decl | const_decl | var_decl | type_decl)* END_OF_FILE {file(pkg, imp, decl)}

import_decl:
    | 'import' '(' x=import+ ')' ';' {x}
    | 'import' x=import ';' {[x]}
import:
    | '.' path=STRING semi {dot_import(path)}
    | alias=IDENT path=STRING semi {alias_import(alias, path)}
    | path=STRING semi {import(path)}

function_decl: 'func' name=IDENT ('[' x=','.generic_parameter+ ','? ']')? '(' p=','.parameter* ','? ')' r=result_decl? b=block? ';' {function_decl(n, t, p, r, b)}
parameter:
    | n=parameter_name '...' t=type {ellipsis_parameter(n, t)}
    | n=parameter_name t=type? {parameter(n, t)}
parameter_name: n=IDENT {parameter_name(n)}
result_decl:
    | '(' s=','.result+ ','? ')' {s}
    | r=type_only_result {[r]}
result:
    | n=result_name t=type? {result(n, t)}
    | type_only_result
type_only_result: t=type {result(_, t)}
result_name: n=IDENT {result_name(n)}

# METHOD DECLARATION ##################

method_decl: 'func' '(' rc=receiver ')' n=method_name '(' p=','.parameter* ','? ')' rs=result_decl? b=block? ';' {method_decl(rc, n, p, rs, b)}
method_name: n=IDENT {method_name(n)}
receiver: n=receiver_name? star='*'? t=receiver_type_name ('[' g=','.receiver_generic_type_name+ ','? ']')? {receiver(n, star, t, g)}
receiver_name: n=IDENT {receiver_name(n)}
receiver_type_name: n=IDENT {receiver_type_name(n)}
receiver_generic_type_name: n=IDENT {receiver_generic_type_name(n)}

# CONST DECLARATION ###################

const_decl:
    | 'const' '(' c=const_spec_semi* ')' ';' {const_decl(c)}
    | 'const' c=const_spec ';' {const_decl([c])}
const_spec_semi: c=const_spec semi {c}
const_spec: i=','.const_name+ (t=type? '=' e=expression_list)? {const_spec(i, t, e)}
const_name: n=IDENT {const_name(n)}

# VAR DECLARATION #####################

var_decl:
    | 'var' '(' x=var_spec_semi* ')' ';' {var_decl(x)}
    | 'var' x=var_spec ';' {var_decl([x])}
var_spec_semi: x=var_spec semi {x}
var_spec: i=','.var_name+  (t=type? '=' e=expression_list)? {var_spec(i, t, e)}
var_name: n=IDENT {var_name(n)}

# TYPE DECLARATION ####################

type_decl:
    | 'type' '(' x=type_spec_semi* ')' ';' {type_decl(x)}
    | 'type' x=type_spec ';' {type_decl([x])}
type_spec_semi: x=type_spec semi {x}
type_spec:
    | x=type_name t=generic_parameter_decl? '=' y=type {type_eq_spec(x, t, y)}
    | x=type_name t=generic_parameter_decl? y=type {type_spec(x, t, y)}

# GENERIC PARAMETER ###################

generic_parameter_decl: '[' x=','.generic_parameter+ ','? ']' {x}
generic_parameter:
    | n=generic_parameter_name t=generic_union_constraint? {generic_parameter(n, t)}
generic_parameter_name: n=IDENT {generic_parameter_name(n)}
generic_union_constraint:
    | x=type_constraint !'|' {x}
    | x='|'.type_constraint+ {generic_union_constraint(x)}
type_constraint:
    | '~' x=type {generic_underlying_type_constraint(x)}
    | x=type {generic_type_constraint(x)}

semi: ';' | &')'| &'}'